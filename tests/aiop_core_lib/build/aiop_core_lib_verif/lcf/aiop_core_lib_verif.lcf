/*****************************************************************************/
/* lcf file for T4240LS processors 											 */
/*****************************************************************************/


MEMORY
{
    hw_context:             org = 0x00000000,   len = 0x000000C0 /* Hardware Context         */
    tls:                    org = 0x000000C0,   len = 0x00000040 /* TLS - Task Local Storage */
    verif_tls:              org = 0x00000100,   len = 0x00000100 /* TLS - For Verification Env. */
    presentation:           org = 0x00000200,   len = 0x00000280 /* Frame presentation area  */
    stack :                 org = 0x00000480,   len = 0x00000380 /* Start location for Stack */
    exception_handlers:     org = 0x00FE0000,   len = 0x00002000 /* IRAM   */
    init:                   org = 0x00FE2000,   len = 0x00001000
    pseudo_rom:             org = 0x00FE3000,   len = 0x00020000
    profile_sram:           org = 0x02030000,	len = 0x0000FFFC /* Profile SRAM */
    private_shared_ram:     org = 0x01000000,   len = 0x00008000 /* Application global vars  */
    verif_shared_ram:       org = 0x01008000,   len = 0x00001000 /* Verification Env global vars  */
    heap  :                 org = 0x01009000,   len = 0x00037000 /* currently sit in shared SRAM */
    dp_ddr:                 org = 0x40000000,   len = 0x10000000 /* DP-DDR  */
    spintbl :               org = 0x50008000,   len = 0x00001000 /* DDR - heap */
    pebm  :                 org = 0x80000000,   len = 0x00600000 /* PEBM */
}


SECTIONS
{
    HWC_PRC_ADDR = 0x50;

    GROUP : {
      .init  : {}
      .init_vle (VLECODE) : {
        *(.init)
        *(.init_vle)
      }
    } > init

    GROUP : {
      .interrupt_vector (VLECODE) ALIGN (2048) : {}
    } > exception_handlers

    GROUP : {
        .verif_text (VLECODE) : {}
        .text (TEXT) ALIGN(0x1000) : {}
        .text_vle (VLECODE) ALIGN(0x1000): {
             *(.text)
             *(.text_vle)
         }
        .itext (TEXT) ALIGN(0x1000): {}
        .itext_vle (VLECODE) ALIGN(0x1000): {
         	*(.itext)
         	*(.itext_vle)
         }
        .rodata (CONST) : {
            *(.rdata)
            *(.rodata)
        }
        .ctors : {}
        .dtors : {}
        extab : {}
        extabindex : {}
    } > pseudo_rom

    GROUP : {
       .sdata  : {}
       .sbss   : {}
       .sdata2 : {}
       .sbss2  : {}
       .data   : {}
       .bss    : {}
       .shdata (DATA) : {}
       .shbss (BSS) NO_INIT_BSS : {}
    } > private_shared_ram

    GROUP : {
        .tdata (DATA):{}
        .tbss (BSS) NO_INIT_BSS :{}
   } > tls

    GROUP : {
       .verif_data   : {}
       .verif_bss   : {}
    } > verif_shared_ram

    GROUP : {
       .verif_tdata   : {}
       .verif_tbss   : {}
    } > verif_tls

    GROUP : {
       .psram_data   : {}
       .psram_bss   : {}
    } > profile_sram
    
    GROUP : {
       .exception_data   : {}
       .exception_data_bss   : {}
    } > dp_ddr

}

TLS_SECTION_END_ADDR = ADDR(verif_tls)+SIZEOF(verif_tls);
_stack_addr  = ADDR(stack)+SIZEOF(stack);
_stack_end   = ADDR(stack);
_heap_addr   = ADDR(heap);
_heap_end    = ADDR(heap)+SIZEOF(heap);
_spin_table  = ADDR(spintbl);

/* Exceptions Handlers Location (used in Exceptions.c for IVPR initialization) */

EXCEPTION_HANDLERS = ADDR(exception_handlers);
